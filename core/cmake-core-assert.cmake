function(ASSERT_EQUAL)
  if(NOT ARGC EQUAL 2)
    message(FATAL_ERROR "assertion failed: invalid argument count: ${ARGC}")
  endif()

  if(NOT "${ARGV0}" STREQUAL "${ARGV1}")
    message(FATAL_ERROR "assertion failed: '${ARGV0}' != '${ARGV1}'")
  endif()
endfunction()

function(ASSERT_NOT_EQUAL)
  if(NOT ARGC EQUAL 2)
    message(FATAL_ERROR "assertion failed: invalid argument count: ${ARGC}")
  endif()

  if("${ARGV0}" STREQUAL "${ARGV1}")
    message(FATAL_ERROR "assertion failed: '${ARGV0}' == '${ARGV1}'")
  endif()
endfunction()

function(ASSERT_EMPTY)
  if(NOT ARGC EQUAL 0)
    message(FATAL_ERROR "assertion failed: input ${ARGC} not empty: '${ARGV}'")
  endif()
endfunction()

function(ASSERT_DEFINED KEY)
  if(NOT DEFINED ${KEY})
    message(FATAL_ERROR "assertion failed: '${KEY}' is not defiend")
  endif()
endfunction()

function(ASSERT_NOT_DEFINED KEY)
  if(DEFINED ${KEY})
    message(FATAL_ERROR "assertion failed: '${KEY}' is defiend (${${KEY}})")
  endif()
endfunction()

function(ASSERT_TRUTHY KEY)
  if(NOT ${${KEY}})
    message(FATAL_ERROR "assertion failed: value of '${KEY}' is not truthy (${${KEY}})")
  endif()
endfunction()

function(ASSERT_FALSY KEY)
  if(${${KEY}})
    message(FATAL_ERROR "assertion failed: value of '${KEY}' is not falsy (${${KEY}})")
  endif()
endfunction()

function(ASSERTION_FAILED)
  message(FATAL_ERROR "assertion failed: ${ARGN}")
endfunction()

function(ASSERT_EXISTS file)
  if(NOT EXISTS ${file})
    message(FATAL_ERROR "assertion failed: file ${file} does not exist")
  endif()
endfunction()

function(ASSERT_NOT_EXISTS file)
  if(EXISTS ${file})
    message(FATAL_ERROR "assertion failed: file ${file} exists")
  endif()
endfunction()
